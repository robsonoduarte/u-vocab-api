plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.5'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'org.sonarqube' version '4.3.0.3225'
	id 'com.avast.gradle.docker-compose' version '0.16.12'
	id 'jacoco'
	id 'groovy'
	id 'com.diffplug.spotless' version '6.20.0'
	id 'com.bmuschko.docker-spring-boot-application' version '9.3.2'
	id "com.google.protobuf" version "0.9.4"
	id "io.freefair.lombok" version "8.3"
	id 'com.palantir.git-version' version '3.0.0'
}

ext.gitVersion = {
	def result = 'git describe --tags --always --dirty'.execute().text.trim()
	if (result.endsWith('-dirty')) {
		return result.replaceAll('-dirty', '-SNAPSHOT')
	}
	return result
}

group = 'com'
version = gitVersion()
sourceCompatibility = '20'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jacoco { toolVersion = "0.8.10" }

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'com.google.protobuf:protobuf-java:3.24.3'
	runtimeOnly 'com.mysql:mysql-connector-j'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.codehaus.groovy:groovy:3.0.18'
	testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

if (!project.hasProperty('noDockerCompose')) {
	dockerCompose {
		stopContainers = true
		removeContainers = true
		removeVolumes = true
		waitForTcpPorts = true
	}
	dockerCompose.isRequiredBy(test)
}

tasks.named('test') {
	useJUnitPlatform()
}

jacocoTestReport {
	reports {
		xml.required = true
	}
	dependsOn test
}

sonar {
	properties {
		property "sonar.projectKey", "robsonoduarte_u-vocab-api"
		property "sonar.organization", "uvocab"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.tests", [
			"${project.projectDir}/src/test/java",
		]
	}
}

tasks.named('sonar').configure {
	dependsOn jacocoTestReport
}

spotless {
	java {
		googleJavaFormat()
		target 'src/**/*.java'
	}
	groovy {
		greclipse()
	}
	groovyGradle {
		greclipse()
	}
}

docker {
	springBootApplication {
		baseImage = 'azul/zulu-openjdk:20.0.2-20.32.11'
	}
}

dockerCreateDockerfile {
	environmentVariable 'DB_USER', System.getenv('DB_USER')
	environmentVariable 'DB_PASSWORD', System.getenv('DB_PASSWORD')
	environmentVariable 'DB_HOST', System.getenv('DB_HOST')
}



sourceSets {
	main {
		proto {
			srcDir 'u-vocab-protobuf/'
		}
	}
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:3.24.3" }
}


allprojects {
	def details = versionDetails()
	println details
}

